" ellesime/trow post style
" Last Update: Feb 16, Sun | 03:36:59 | 2014

" substitution "{{{1
" character
function! Title_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" markdown bracket
				1g/^\[markdown\]$/delete
				$g/^\[\/markdown\]$/delete
			" main title
				g/^#\{2\}\s/s/$/[\/size]\r[hr]/e
				%s/^#\{2\}\s/[size=18pt]/e
			" sub title 1
				g/^#\{3\}\s/s/$/[\/size]/e
				%s/^#\{3\}\s/[size=14pt]/e
			" sub title 2
				g/^#\{4\}\s/s/$/[\/size]/e
				%s/^#\{4\}\s/[size=12pt]/e
	" trow style
		elseif a:forum==1
			" markdown bracket
				1s/^/[markdown]\r
				$s/$/\r[\/markdown]
			" main title
				g/\[size=18pt\]/.+1s/^\[hr\]$//e
				%s/^\[size=18pt\]/## /e
			" sub title 1
				%s/^\[size=14pt\]/### /e
			" sub title 2
				%s/^\[size=12pt\]/#### /e
			" clear [/size]
				%s/\[\/size\]//e
		endif
endfunction "}}}
function! Paragraph_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" seperation line
				%s/^_\{3,\}$/[hr]/e
			" bullet point *
				%s/^\*\s/[list][li]/e
			" bullet point +
				%s/^\(\s\{4\}\|\t\)+\s/[list][list][li]/e
			" bullet text
				" text under '+'
					g/^\(\[list\]\)\{2\}/.+1s/^\(\s\{4\}\|\t\)/[list][list]/e
				" text under '*' and other indented lines
					%s/^\(\s\{4\}\|\t\)/[list]/e
			" [/list]
				g/^\[list\]\[list\]\[li\]/s/$/[\/li][\/list][\/list]/e
				g/^\[list\]\[list\]\(\[li\]\)\@!/s/$/[\/list][\/list]/e
				g/^\(\[list\]\)\@<!\[list\]\[li\]/s/$/[\/li][\/list]/e
				g/^\(\[list\]\)\@<!\[list\]\(\[li\]\|\[list\]\)\@!/s/$/[\/list]/e
	" trow style
		elseif a:forum==1
			" seperation line
				%s/^\[hr\]$/__________/e
			" bullet point *
				g/\[list\]/left 0
				%s/^\[list\]\[li\]/* /e
			" bullet point +
				%s/^\(\[list\]\)\{2\}\[li\]/\t+ /e
			" bullet text
				%s/^\(\[list\]\)\{1,2\}/\t/e
			" clear [/li][/list]
				%s/\(\[\/li\]\|\)\(\[\/list\]\)\{1,2\}$//e
		endif
endfunction "}}}
function! Character_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" hyper-link, [text](url) <--> [url=]text[/url]
			" cut brackets '[]'
				%s/\[/\r[/ge
				%s/\]/]\r/ge
			" fix broken ones like '[text1[text2]text3]
				g/^\[.*\(\]\)\@<!$/.,.+1join!
				g/^\(\[\)\@!.*\]$/.-1,.join!
			" cut url
				g/http:/s/^\((http:.\{-\})\)/\1\r/e
			" join [text] and (url)
				g/^(http:/.-1,.join!
			" change style
				g/(http:/s/^\[\(.\{-\}\)\](\(http:.\{-}\))$/[url=\2]\1[\/url]/e
			" join lines begin/end with brackets '[]'
				g/^\[/.-1,.join!
				g/\]$/.,.+1join!
			" bold
				%s/\(\*\{2\}.\{-\}\*\{2\}\)/\r\1\r/ge
				%s/^\*\{2\}\(.\{-\}\)\*\{2\}$/[b]\1[\/b]/e
				g/^\[b\]/.-1,.+1join!
			" italic
				%s/\(\*.\{-\}\*\)/\r\1\r/ge
				%s/^\*\(.\{-\}\)\*$/[i]\1[\/i]/ge
				g/^\[i\]/.-1,.+1join!
	" trow style
		elseif a:forum==1
			" hyper-link
				%s/\(\[url=.\{-\}\/url]\)/\r\1\r/ge
				g/\[url=/s/^\[url=\(http:.\{-\}\)\]\(.\{-\}\)\[\/url\]$/[\2](\1)/e
				g/\](http:/.-1,.+1join!
			" bold
				%s/\(\[b\].\{-\}\[\/b\]\)/\r\1\r/ge
				%s/^\[b\]\(.\{-\}\)\[\/b\]/**\1**/e
				g/^\*\{2\}.\{-\}\*\{2\}$/.-1,.+1join!
			" italic
				%s/\(\[i\].\{-\}\[\/i\]\)/\r\1\r/ge
				%s/^\[i\]\(.\{-\}\)\[\/i\]/*\1*/e
				g/^\*[^\*].\{-\}[^\*]\*$/.-1,.+1join!
		endif
endfunction "}}}
" }}}1

" main function "{{{1
function! Ellesime_ET() "{{{
	call Character_ET(0)
	call Title_ET(0)
	call Paragraph_ET(0)
endfunction "}}}
function! Trow_ET() "{{{
	call Character_ET(1)
	call Title_ET(1)
	call Paragraph_ET(1)
endfunction "}}}
" }}}1

" command "{{{1
command! PoEllesime call Ellesime_ET()
command! PoTrow call Trow_ET()
" }}}1
" vim: set nolinebreak number foldlevel=20:

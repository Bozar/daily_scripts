" ellesime/trow post style
" Last Update: Feb 15, Sat | 23:45:49 | 2014

" substitution "{{{1
" character
function! Title_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" markdown bracket
				1g/^\[markdown\]$/delete
				$g/^\[\/markdown\]$/delete
			" main title
				g/^#\{2\}\s/s/$/[\/size]\r[hr]/e
				%s/^#\{2\}\s/[size=18pt]/e
			" sub title
				g/^#\{3\}\s/s/$/[\/size]/e
				%s/^#\{3\}\s/[size=14pt]/e
	" trow style
		elseif a:forum==1
			" markdown bracket
				1s/^/[markdown]\r
				$s/$/\r[\/markdown]
			" main title
				g/\[size=18pt\]/.+1s/^\[hr\]$//e
				%s/^\[size=18pt\]/## /e
			" sub title
				%s/^\[size=14pt\]/### /e
			" clear [/size]
				%s/\[\/size\]//e
		endif
endfunction "}}}
function! Paragraph_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" seperation line
				%s/^_\{3,\}$/[hr]/e
			" bullet point *
				g/^\*\s/s/$/[\/li][\/list]/e
				%s/^\*\s/[list][li]/e
			" bullet point +
				g/^\(\s\{4\}\|\t\)+\s/s/$/[\/li][\/list][\/list]/e
				%s/^\(\s\{4\}\|\t\)+\s/[list][list][li]/e
			" bullet text
			" texts under both bullets are substituted into the same pattern
			" it is a bug that I can't fix
				g/^\(\s\{4\}\|\t\)/s/$/[\/list]/e
				%s/^\(\s\{4\}\|\t\)/[list]/e
	" trow style
		elseif a:forum==1
			" seperation line
				%s/^\[hr\]$/__________/e
			" bullet point *
				g/\[list\]/left 0
				%s/^\[list\]\[li\]/* /e
			" bullet point +
				%s/^\(\[list\]\)\{2\}\[li\]/\t+ /e
			" bullet text
				%s/^\(\[list\]\)\{1,2\}/\t/e
			" clear [/li][/list]
				%s/\(\[\/li\]\|\)\(\[\/list\]\)\{1,2\}$//e
		endif
endfunction "}}}
function! Character_ET(forum) "{{{
	" ellesime style
		if a:forum==0
			" hyper-link
			" avoid this style: [text1]text2[text3](url)
			" while this is allowed: [text1[text2]text3](url)
				%s/\(\[.\{-\}\](http:.\{-\})\)/\r\1\r/ge
				g/(http:/s/^\[\(.\{-\}\)\](\(http:.\{-}\))$/[url=\2]\1[\/url]/e
				g/\[url=http:/.-1,.+1join!
			" bold
				%s/\(\*\{2\}.\{-\}\*\{2\}\)/\r\1\r/ge
				%s/^\*\{2\}\(.\{-\}\)\*\{2\}$/[b]\1[\/b]/e
				g/^\[b\]/.-1,.+1join!
			" italic
				%s/\(\*.\{-\}\*\)/\r\1\r/ge
				%s/^\*\(.\{-\}\)\*$/[i]\1[\/i]/ge
				g/^\[i\]/.-1,.+1join!
	" trow style
		elseif a:forum==1
			" hyper-link
				%s/\(\[url=.\{-\}\/url]\)/\r\1\r/ge
				g/\[url=/s/^\[url=\(http:.\{-\}\)\]\(.\{-\}\)\[\/url\]$/[\2](\1)/e
				g/\](http:/.-1,.+1join!
			" bold
				%s/\(\[b\].\{-\}\[\/b\]\)/\r\1\r/ge
				%s/^\[b\]\(.\{-\}\)\[\/b\]/**\1**/e
				g/^\*\{2\}.\{-\}\*\{2\}$/.-1,.+1join!
			" italic
				%s/\(\[i\].\{-\}\[\/i\]\)/\r\1\r/ge
				%s/^\[i\]\(.\{-\}\)\[\/i\]/*\1*/e
				g/^\*[^\*].\{-\}[^\*]\*$/.-1,.+1join!
		endif
endfunction "}}}
" }}}1

" main function "{{{1
function! Ellesime_ET() "{{{
	call Title_ET(0)
	call Paragraph_ET(0)
	call Character_ET(0)
endfunction "}}}
function! Trow_ET() "{{{
	call Title_ET(1)
	call Paragraph_ET(1)
	call Character_ET(1)
endfunction "}}}
" }}}1

" command "{{{1
command! PoEllesime call Ellesime_ET()
command! PoTrow call Trow_ET()
" }}}1
" vim: set nolinebreak number foldlevel=20:
